# Copyright The OpenTelemetry Authors
# SPDX-License-Identifier: Apache-2.0

include_guard()

# Aggregate target to copy all generated Rust crates (apps + aggregator)
if(NOT TARGET render_source_dir)
  add_custom_target(render_source_dir)
endif()

function(render_compile INPUT_DIR)
  cmake_parse_arguments(ARG "" "OUTPUT_DIR;PACKAGE;COMPILER" "APPS;DEPENDS" ${ARGN})

  if(DEFINED ARG_OUTPUT_DIR)
    set(OUTPUT_DIR ${ARG_OUTPUT_DIR})
  else()
    set(OUTPUT_DIR "${CMAKE_BINARY_DIR}/generated")
  endif()

  if(DEFINED ARG_PACKAGE)
    set(PACKAGE ${ARG_PACKAGE})
  else()
    set(PACKAGE "default")
  endif()

  if(DEFINED ARG_COMPILER)
    set(RENDER_COMPILER ${ARG_COMPILER})
  else()
    get_property(
      RENDER_COMPILER
      TARGET
        render_compiler
      PROPERTY
        RENDER_COMPILER_PATH
    )
  endif()

  set(RENDER_${PACKAGE}_OUTPUTS "")

  foreach(APP ${ARG_APPS})
    set(
      RENDER_${PACKAGE}_${APP}_DESCRIPTOR
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/descriptor.cc"
    )
    set(
      RENDER_${PACKAGE}_${APP}_HASH
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/hash.c"
    )
    set(
      RENDER_${PACKAGE}_${APP}_WRITER
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/writer.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/writer.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/encoder.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/encoder.cc"
    )
    set(
      RENDER_${PACKAGE}_${APP}_OUTPUTS
        ${RENDER_${PACKAGE}_${APP}_HASH}
        ${RENDER_${PACKAGE}_${APP}_DESCRIPTOR}
        ${RENDER_${PACKAGE}_${APP}_WRITER}
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/index.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/index.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/containers.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/containers.inl"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/containers.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/keys.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/handles.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/handles.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/auto_handles.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/auto_handles.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/weak_refs.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/weak_refs.inl"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/weak_refs.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/modifiers.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/modifiers.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/spans.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/spans.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/span_base.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/connection.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/connection.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/protocol.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/protocol.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/transform_builder.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/transform_builder.cc"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/parsed_message.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/wire_message.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/meta.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/bpf.h"
        # Rust per-app crate files live under src/
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/src/wire_messages.rs"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/src/parsed_message.rs"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/src/encoder.rs"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/src/hash.rs"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/Cargo.toml"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/src/lib.rs"
        # Headers that are also generated alongside sources
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/descriptor.h"
        "${OUTPUT_DIR}/${PACKAGE}/${APP}/hash.h"
    )
    list(
      APPEND
      RENDER_${PACKAGE}_OUTPUTS
        ${RENDER_${PACKAGE}_${APP}_OUTPUTS}
    )
  endforeach()

  # Per-package Rust aggregator crate outputs (generated by the render compiler)
  list(APPEND RENDER_${PACKAGE}_OUTPUTS "${OUTPUT_DIR}/${PACKAGE}/Cargo.toml")
  list(APPEND RENDER_${PACKAGE}_OUTPUTS "${OUTPUT_DIR}/${PACKAGE}/src/lib.rs")

  list(
    APPEND
    RENDER_${PACKAGE}_OUTPUTS
      "${OUTPUT_DIR}/${PACKAGE}/metrics.h"
  )

  set_source_files_properties(
    ${RENDER_${PACKAGE}_OUTPUTS}
    PROPERTIES
      GENERATED TRUE
  )

  file(
    GLOB
    RENDER_INPUTS
      "${INPUT_DIR}/*.render"
  )

  # Generate sources
  #
  add_custom_command(
    OUTPUT
      ${RENDER_${PACKAGE}_OUTPUTS}
    WORKING_DIRECTORY
      ${INPUT_DIR}
    COMMAND
      ${CMAKE_COMMAND} -E make_directory "${OUTPUT_DIR}"
    COMMAND
      java -jar ${RENDER_COMPILER} -i . -o "${OUTPUT_DIR}"
    DEPENDS
      ${RENDER_INPUTS}
      ${RENDER_COMPILER}
      ${ARG_DEPENDS}
  )
  add_custom_target(
    render_compile_${PACKAGE}
    DEPENDS
      ${RENDER_${PACKAGE}_OUTPUTS}
  )

  # Generated sources interface library
  #
  add_library(
    render_${PACKAGE}_artifacts
    INTERFACE
  )
  target_include_directories(
    render_${PACKAGE}_artifacts
    INTERFACE
      "${OUTPUT_DIR}"
  )
  add_dependencies(
    render_${PACKAGE}_artifacts
      render_compile_${PACKAGE}
  )

  # Generated sources app libraries
  #
  foreach(APP ${ARG_APPS})
    add_library(
      render_${PACKAGE}_${APP}
      STATIC
      EXCLUDE_FROM_ALL
        ${RENDER_${PACKAGE}_${APP}_OUTPUTS}
    )
    target_include_directories(
      render_${PACKAGE}_${APP}
      PUBLIC
        ${OUTPUT_DIR}
    )
    target_link_libraries(
      render_${PACKAGE}_${APP}
      PUBLIC
        fixed_hash
        render_${PACKAGE}_artifacts
    )

    add_library(
      render_${PACKAGE}_${APP}_hash
      STATIC
      EXCLUDE_FROM_ALL
        ${RENDER_${PACKAGE}_${APP}_HASH}
    )
    target_include_directories(
      render_${PACKAGE}_${APP}_hash
      PUBLIC
        ${OUTPUT_DIR}
    )
    target_link_libraries(
      render_${PACKAGE}_${APP}_hash
      PUBLIC
        render_${PACKAGE}_artifacts
    )

    add_library(
      render_${PACKAGE}_${APP}_descriptor
      STATIC
      EXCLUDE_FROM_ALL
        ${RENDER_${PACKAGE}_${APP}_DESCRIPTOR}
    )
    target_include_directories(
      render_${PACKAGE}_${APP}_descriptor
      PUBLIC
        ${OUTPUT_DIR}
    )
    target_link_libraries(
      render_${PACKAGE}_${APP}_descriptor
      PUBLIC
        render_${PACKAGE}_artifacts
    )

    add_library(
      render_${PACKAGE}_${APP}_writer
      STATIC
      EXCLUDE_FROM_ALL
        ${RENDER_${PACKAGE}_${APP}_WRITER}
    )
    target_include_directories(
      render_${PACKAGE}_${APP}_writer
      PUBLIC
        ${OUTPUT_DIR}
    )
    target_link_libraries(
      render_${PACKAGE}_${APP}_writer
      PUBLIC
        render_${PACKAGE}_artifacts
    )

  endforeach()

  # Build a single Rust aggregator staticlib from the source workspace (crates/),
  # not from the generated/ tree. The aggregator crate is expected at
  # ${PROJECT_SOURCE_DIR}/crates/render/${PACKAGE} and is updated manually via
  # the render_source_dir targets below.
  string(REPLACE ":" "_" _pkg_safe "${PACKAGE}")
  set(RUST_AGG_DIR "${PROJECT_SOURCE_DIR}/crates/render/${PACKAGE}")

  # Build the aggregator staticlib
  set(RUST_AGG_TARGET_DIR "${CMAKE_BINARY_DIR}/cargo/${PACKAGE}-agg")
  set(RUST_AGG_CRATE_NAME "encoder_${_pkg_safe}_all")
  set(RUST_AGG_STATICLIB "${RUST_AGG_TARGET_DIR}/release/lib${RUST_AGG_CRATE_NAME}.a")

  add_custom_command(
    OUTPUT "${RUST_AGG_STATICLIB}"
    WORKING_DIRECTORY "${RUST_AGG_DIR}"
    COMMAND ${CMAKE_COMMAND} -E env CARGO_TARGET_DIR=${RUST_AGG_TARGET_DIR} RUSTFLAGS=-Cpanic=abort cargo build --release
    VERBATIM
  )
  # Target `render_rust_${PACKAGE}_build` depends on the library being built
  add_custom_target(render_rust_${PACKAGE}_build
    DEPENDS "${RUST_AGG_STATICLIB}")

  # Users can add `render_rust_${PACKAGE}` to their link libraries to link the Rust staticlib
  add_library(render_rust_${PACKAGE} STATIC IMPORTED GLOBAL)
  set_target_properties(render_rust_${PACKAGE} PROPERTIES IMPORTED_LOCATION "${RUST_AGG_STATICLIB}")
  add_dependencies(render_rust_${PACKAGE} render_rust_${PACKAGE}_build)

  # Note: do not implicitly link the Rust staticlib into writer libs.
  # Consumers that need the Rust aggregator (e.g., reducer) should link
  # target `render_rust_${PACKAGE}` explicitly.

  # Developer-only: copy generated Rust crates into the source tree for review/commit
  # This target is safe in CI because it is not part of the default build.
  # Destination will be: ${PROJECT_SOURCE_DIR}/crates/render/${PACKAGE}/${APP}
  set(_render_copy_targets)
  foreach(APP ${ARG_APPS})
    set(_src_dir  "${OUTPUT_DIR}/${PACKAGE}/${APP}")
    set(_dest_dir "${PROJECT_SOURCE_DIR}/crates/render/${PACKAGE}/${APP}")
    add_custom_target(
      copy_render_crate_${PACKAGE}_${APP}
      COMMAND ${CMAKE_COMMAND} -E remove_directory "${_dest_dir}"
      COMMAND ${CMAKE_COMMAND} -E make_directory "${_dest_dir}/src"
      COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_src_dir}/Cargo.toml" "${_dest_dir}/Cargo.toml"
      COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_src_dir}/src/lib.rs" "${_dest_dir}/src/lib.rs"
      COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_src_dir}/src/encoder.rs" "${_dest_dir}/src/encoder.rs"
      COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_src_dir}/src/wire_messages.rs" "${_dest_dir}/src/wire_messages.rs"
      COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_src_dir}/src/parsed_message.rs" "${_dest_dir}/src/parsed_message.rs"
      COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_src_dir}/src/hash.rs" "${_dest_dir}/src/hash.rs"
      DEPENDS render_compile_${PACKAGE}
      VERBATIM
    )
    list(APPEND _render_copy_targets copy_render_crate_${PACKAGE}_${APP})
  endforeach()

  # Also copy the generated aggregator crate into the source tree at
  # ${PROJECT_SOURCE_DIR}/crates/render/${PACKAGE}
  set(_agg_src_dir  "${OUTPUT_DIR}/${PACKAGE}")
  set(_agg_dest_dir "${PROJECT_SOURCE_DIR}/crates/render/${PACKAGE}")
  add_custom_target(
    copy_render_crate_${PACKAGE}_aggregator
    COMMAND ${CMAKE_COMMAND} -E make_directory "${_agg_dest_dir}/src"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_agg_src_dir}/Cargo.toml" "${_agg_dest_dir}/Cargo.toml"
    COMMAND ${CMAKE_COMMAND} -E copy_if_different "${_agg_src_dir}/src/lib.rs" "${_agg_dest_dir}/src/lib.rs"
    DEPENDS render_compile_${PACKAGE}
    VERBATIM
  )
  list(APPEND _render_copy_targets copy_render_crate_${PACKAGE}_aggregator)
  # Developer entrypoint per package to avoid name collisions with other projects
  add_custom_target(render_source_dir_${PACKAGE})
  add_dependencies(render_source_dir_${PACKAGE} ${_render_copy_targets})
  add_dependencies(render_source_dir render_source_dir_${PACKAGE})
endfunction()
